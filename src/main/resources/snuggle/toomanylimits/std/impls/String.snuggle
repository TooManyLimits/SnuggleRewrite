import "std/types/Inc"
import "std/impls/iterator"

pub impl String {

    // .string() method. Just returns itself.
    fn string(): String this

    // Overload the # operator .size() to get the length
    // of the string in chars.
    fn size(): u32 this.length

    // Operator overload to check equality of strings:
    fn eq(other: String): bool {
        // If length is different, immediately rule it out
        if #this != #other return false
        // Iterate over the chars. If any is different, return false.
        let mut i: u32 = 0
        while i < length {
            if this[start + i] != other[other.start + i]
                return false
            i = i + 1
        }
        // Otherwise, return true.
        return true
    }

    // Concatenate this string and the other object into a new string.
    // The other object is required to have a `.string()` method.
    fn add<T>(other: T): String {
        let otherString: String = other.string()
        let chars = new char[](#this + #otherString)
        for (i, c) in this.chars().indexed() {
            chars[i] = c
        }
        for (i, c) in otherString.chars().indexed() {
            chars[#this + i] = c
        }
        new(chars, 0, #chars)
    }

    // Get an iterator over the characters of this string.
    fn chars(): () -> char? {
        // Create an incrementor and a closure
        let i = new Inc<u32>()
        fn() {
            if *i >= #this {
                // If we've reached the end of the string, return empty
                new()
            } else {
                // Otherwise, return a full option of the i'th character,
                // and increment i.
                new(this[i()])
            }
        }
    }

}
